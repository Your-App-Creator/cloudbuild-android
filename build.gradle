// project-wide `build.gradle`

buildscript {
    ext {
        android_gradle_plugin_version = '8.2.1'
        kotlin_version = '1.9.21'
        junit_version = '4.13.2'

        firebase_crashlytics_version = '18.6.0'
        firebase_perf_version = '20.5.1'

        androidx_appcompat_version = '1.6.1'
        androidx_constraintlayout_version = '2.1.4'
        androidx_navigation_version = '2.7.6'
        
        androidx_test_junit_version = '1.1.5'
        androidx_test_core_version = '1.5.0'
        androidx_test_monitor_version = '1.6.1'
        androidx_test_runner_version = '1.5.2'
        androidx_test_rules_version = '1.5.0'
        androidx_test_espresso_version = '3.5.1'
        androidx_test_uiautomator_version = '2.2.0'
    }

    // conditional remote dependency, when directory `buildSrc` is absent.
    if (! new File(getRootDir().absolutePath + "/buildSrc").exists()) {
        dependencies {
            classpath 'io.syslogic:google-cloud-kms-gradle-plugin:1.0.8'
        }
    }
}

plugins {
    id 'com.android.application' version "$android_gradle_plugin_version" apply false
    // id 'com.android.library' version "$android_gradle_plugin_version" apply false
    id 'androidx.navigation.safeargs' version "$androidx_navigation_version" apply false
    id 'com.google.gms.google-services' version "4.4.0" apply false
    id 'com.google.firebase.appdistribution' version "4.0.1" apply false
    id 'com.google.firebase.firebase-perf' version "1.4.2" apply false
    id 'com.google.firebase.crashlytics' version "2.9.9" apply false
}

// Keystore Settings, loaded from keystore.properties
if (rootProject.file('keystore.properties').exists()) {
    def keystore = new Properties()
    keystore.load(new FileInputStream(rootProject.file('keystore.properties')))
    project.ext.set('debugKeystorePass',   keystore['debugKeystorePass'])
    project.ext.set('debugKeyAlias',       keystore['debugKeyAlias'])
    project.ext.set('debugKeyPass',        keystore['debugKeyPass'])
    project.ext.set('releaseKeystorePass', keystore['releaseKeystorePass'])
    project.ext.set('releaseKeyAlias',     keystore['releaseKeyAlias'])
    project.ext.set('releaseKeyPass',      keystore['releaseKeyPass'])
} else {
    logger.error('> File keystore.properties is missing.')
}

/** Modules */
allprojects {
    configurations.configureEach {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'org.jetbrains.kotlin') {
                List<String> list = ['kotlin-stdlib', 'kotlin-stdlib-jdk7', 'kotlin-stdlib-jdk8', 'kotlin-stdlib-common']
                if (list.contains(requested.name)) {
                    details.useVersion "$kotlin_version"
                }
            }
        }
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile).tap {
            configureEach {
                options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
            }
        }
    }
}

// rootProject > clean
tasks.register('clean', Delete) {

    // cleanup Gradle logs
    def gradle = project.getGradle()
    new File("${gradle.getGradleUserHomeDir().getAbsolutePath()}/daemon/${gradle.getGradleVersion()}").listFiles().each {
        if (it.getName().endsWith('.out.log')) {
            println(":clean removed: $it")
            it.delete()
        }
    }

    // delete build directories
    delete rootProject.fileTree('build')
    delete project.fileTree('build')
}
